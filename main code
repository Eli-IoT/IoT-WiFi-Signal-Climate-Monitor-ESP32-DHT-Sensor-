#include <Adafruit_Sensor.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <DHT.h>
#include "secrets.h"

#define DHTPIN 4
#define DHTTYPE DHT22  // Pas aan naar DHT11 als je die gebruikt
DHT dht(DHTPIN, DHTTYPE);

void setup() {
  Serial.begin(115200);
  dht.begin();
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Verbinden...");
  }
  Serial.println("Verbonden met WiFi!");
}

void loop() {
  int n = WiFi.scanNetworks();
  String bestSSID = "Onbekend";
  int bestRSSI = -9999;

  for (int i = 0; i < n; i++) {
    if (WiFi.RSSI(i) > bestRSSI && WiFi.SSID(i).length() > 0) {
      bestRSSI = WiFi.RSSI(i);
      bestSSID = WiFi.SSID(i);
    }
  }

  float temp = dht.readTemperature();
  float hum = dht.readHumidity();
  if (isnan(temp)) temp = -100;
  if (isnan(hum)) hum = -1;

  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    String url = "http://api.thingspeak.com/update?api_key=" + String(THINGSPEAK_API_KEY) +
                 "&field1=" + String(bestRSSI) +
                 "&field2=" + String(temp) +
                 "&field3=" + String(hum);
    http.begin(url);
    int httpCode = http.GET();
    http.end();
    Serial.println("Data verzonden!");
  }

  delay(15000);
}
